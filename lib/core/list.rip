# class (System.Enumerable) {
class {
	@.+ = -> (other) {
		@.class.+(@, other)
	}

	@.<< = -> (new_item) {
		@ + [ new_item ]
	}

	@.filter = -> (sieve) {
		@.class.filter(sieve, @)
	}

	@.fold = -> (initial, folder) {
		@.class.fold(folder, initial, @)
	}

	@.map = -> (mapper) {
		@.class.map(mapper, @)
	}

	filter = -> (sieve, items) {
		fold(-> (memo, item) {
			if (sieve(item)) {
				memo << item
			} else {
				memo
			}
		}, [], items)
	}

	fold = -> (folder, initial, items) {
		if (items.length == 0) {
			initial
		} else {
			fold(folder, folder(initial, items.head), items.tail)
		}
	}

	map = -> (mapper, items) {
		fold(-> (memo, item) {
			memo << mapper(item)
		}, [], items)
	}

	to_string = -> { 'System.List' }
}
